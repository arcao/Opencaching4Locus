apply plugin: 'com.android.application'
// apply plugin: 'io.fabric'

dependencies {
    def supportVer = '26.0.1'

    // Android Support
    //noinspection GradleCompatible
    implementation "com.android.support:support-v4:$supportVer"
    implementation "com.android.support:appcompat-v7:$supportVer"
    implementation "com.android.support:recyclerview-v7:$supportVer"
    implementation "com.android.support:cardview-v7:$supportVer"
    implementation "com.android.support:design:$supportVer"

    // Locus API
    implementation 'com.asamm:locus-api-android:0.2.10'

    // Logging API
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // Apache commons
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'commons-io:commons-io:2.5'

    // ScribeJava
    def scribeJavaVer = "4.2.0"
    implementation "com.github.scribejava:scribejava-core:$scribeJavaVer"
    implementation "com.github.scribejava:scribejava-httpclient-okhttp:$scribeJavaVer"

    // Material dialogs
    implementation 'com.afollestad.material-dialogs:core:0.9.4.7'

    // Crashlytics
    // implementation 'com.crashlytics.sdk.android:crashlytics:2.6.8'

    // Networking
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'

    // Lifecyles, LiveData and ViewModel
    def lifecycleVer = "1.0.0-alpha9"
    implementation "android.arch.lifecycle:runtime:$lifecycleVer"
    implementation "android.arch.lifecycle:extensions:$lifecycleVer"
    annotationProcessor "android.arch.lifecycle:compiler:$lifecycleVer"

    // Auto-Value
    def autoValueVer = '1.5'
    compileOnly "com.google.auto.value:auto-value:$autoValueVer"
    annotationProcessor "com.google.auto.value:auto-value:$autoValueVer"

    // Auto-Value Parcelable
    def autoValueParcelableVer = '0.2.5'
    compileOnly "com.ryanharter.auto.value:auto-value-parcel-adapter:$autoValueParcelableVer"
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:$autoValueParcelableVer"

    // Dagger 2
    def daggerVer = '2.11'
    compileOnly "com.google.dagger:dagger-android:$daggerVer"
    implementation "com.google.dagger:dagger-android-support:$daggerVer"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVer"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVer"
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    dataBinding {
        enabled = true
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries preDexEnabled && !isTravis
    }

    defaultConfig {
        minSdkVersion hasProperty('devBuild') ? 26 : 19
        targetSdkVersion 26
        versionCode gitVersionCode()
        archivesBaseName = parent.name
        buildConfigField 'String', 'GIT_SHA', '"' + gitSha() +'"'
        buildConfigField 'String', 'BUILD_TIME', '"' + gitTimestamp() + '"'

        resConfigs 'en'
    }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias = 'opencaching4locus'
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
            versionNameSuffix '-dev'
        }
        release {
            versionNameSuffix ''

            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                './proguard-rules/proguard-autovalue.pro',
                './proguard-rules/proguard-crashlytics-2.pro',
                './proguard-rules/proguard-java-serializable.pro',
                './proguard-rules/proguard-project.pro',
                './proguard-rules/proguard-project-feedback.pro',
                './proguard-rules/proguard-scribejava.pro',
                './proguard-rules/proguard-square-okhttp3.pro',
                './proguard-rules/proguard-square-okio.pro',
                './proguard-rules/proguard-support-v7-appcompat.pro',
                './proguard-rules/proguard-support-v7-cardview.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'org/apache/http/version.properties'
        exclude 'templates/release-notes.vm'
        exclude 'log4j.xml'
    }
    lintOptions { abortOnError false }
}

if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = android.signingConfigs.debug
}